plugins {
  id 'java'
}

version '1.0'

jar {
  // by default fat jar
  archiveName = 'vertx-blueprint-todo-backend-fat'
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  manifest {
    attributes 'Main-Class': 'io.vertx.core.Launcher'
    attributes 'Main-Verticle': 'io.vertx.blueprint.todolist.verticles.TodoVerticle'
  }
}

repositories {
  maven { // snapshot repo, will be removed after 3.3 version released
    url "https://oss.sonatype.org/content/repositories/snapshots"
  }
  jcenter()
  mavenCentral()
  mavenLocal()
}

task annotationProcessing(type: JavaCompile, group: 'build') {
  source = sourceSets.main.java
  classpath = configurations.compile
  destinationDir = project.file('src/main/generated')
  options.compilerArgs = [
    "-proc:only",
    "-processor", "io.vertx.codegen.CodeGenProcessor",
    "-AoutputDirectory=${destinationDir.absolutePath}"
  ]
}

sourceSets {
  main {
    java {
      srcDirs += 'src/main/generated'
    }
  }
}

compileJava {
  targetCompatibility = 1.8
  sourceCompatibility = 1.8

  dependsOn annotationProcessing
}

dependencies {
  compile "io.vertx:vertx-core:3.3.0-SNAPSHOT"
  compile 'io.vertx:vertx-web:3.2.1'
  compile 'io.vertx:vertx-jdbc-client:3.2.1'
  compile 'mysql:mysql-connector-java:6.0.2'
  compile 'io.vertx:vertx-redis-client:3.2.1'
  compile 'io.vertx:vertx-codegen:3.2.1'

  testCompile 'io.vertx:vertx-unit:3.2.1'
  testCompile group: 'junit', name: 'junit', version: '4.12'
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.12'
}